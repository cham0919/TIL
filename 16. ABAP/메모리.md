# 메모리

<br/>

### SAP Memory

- 사용자 터미널 세션 내 모든 외부 세션에서 접근할 수 있는 글로벌 메모리
- SAP 메모리의 데이터를 주고받으려면 SET/GET 구문을 이용해 메모리 파라미터를 전달한다

<br/>


### ABAP Memory

- 같은 외부 새션 내에서 수행되고 있는 모든 프로그램에서 공유되는 메모리
- 개별 외부 세션에 하나만 존재
- EXPORT/  IMPORT 를 사용해 데이터를 주고 받는다

<br/>

### 프로그램 간 데이터 전달법

- Interface
   -  TYPE-1 프로그램 호출할 때, SUBMIT 구문을 이용해 Selection Screen의 인풋 필드에 값 입력 혹은 Variant 호출하는 방법

- ABAP Memory
   - EXPORT/  IMPORT 를 사용해 데이터를 주고 받으며 같은 파라미터 id 명을 사용해야한다

- SAP Memory
   - 파라미터 id를 이용해 데이터를 넘겨준다

- Database Table
   - 데이터베이스의 데이터를 이용해 데이터를 전달한다

- Presentation Server
   - SAPgui가 실행 중인 로컬 pc에 데이터를 파일로 다운로드했다가 업로드 하는 방법을 의미

<br/>

### SAP 메모리 할당

- 필드 <f>를 SPA/GPA 파라미터 <pid>에 저장 시 다음 구문을 사용한다.
- 이미 저장되어있다면 덮어쓴다.
- ```SET PARAMETER ID <pid> FIELD <f>.```

<br/>

### SAP 메모리 읽기

-  <pid>에 저장된 값을 필드 <f>에 저장하게 된다. id가 존재하지 않으면 sy-subrc에 4가, 존재하면 0이 할당된다
- ```GET PARAMETER ID <pid> FIELD <f>.```

<br/>

### TYPE-1 프로그램의 메모리 ID 관리

- TYPE-1 리포트 프로그램의 Selection Screen에서 파라미터 또는 Select-Option 변수에 메모리 id를 사용해 필드와 파라미터를 연결한다
-  SET/GET과 같은 기능
- 다른 세션에서 조회 가능

```
PARAMETERS : p_a TYPE c MEMORY ID zscl.

WRITE : / p_a.

TABLES sbook.

START-OF-SELECTION.
  WRITE :  'Double Click a booking List.',
      / '---------------------------------------'.
   SKIP.
   SELECT * FROM sbook UP TO 5 ROWS.

     WRITE: / sbook-carrid, sbook-connid,
     sbook-fldate, sbook-bookid.
*     필드의 값을 HIDE 메모리 영역으로 저장하는 역할 수행하며 WRITE 구문 바로 아래 기술
     HIDE : sbook-carrid, sbook-connid,
     sbook-fldate, sbook-bookid.

   ENDSELECT.
```

<br/>


-  라인 선택 시, 파라미터 할당

```
   AT LINE-SELECTION.
      SET PARAMETER ID: 'CAR' FIELD sbook-carrid,
      'CON' FIELD sbook-connid.

```

<br/>

-  트랜잭션 호출 및 현재 화면 숨김

```
    CALL TRANSACTION 'YCHS_16' AND SKIP FIRST SCREEN.
```

<br/><br/>


### ABAP 메모리

- EXPORT 이용해 데이터 오브젝트를 메모리에 로드 가능.
- 내부 세션에서만 데이터 공유가 가능해 다른 외부 세션 프로그램에서는 넘겨줄 수 없다
- 즉, 같은 윈도우 창에서만 메모리 공유하고 새 창을 띄우는 것은 공유되지 않는다

- 내부 세션 내 프로그램 간 인터널 테이블 데이터 넘겨주는 과정 예제


```
TABLES : sflight.

DATA : gt_sflight TYPE TABLE OF sflight.

SELECT *
 INTO CORRESPONDING FIELDS OF TABLE gt_sflight
 FROM sflight
  WHERE carrid = 'AA'.

  EXPORT gt_sflight TO MEMORY ID 'TEST_ID'.

*  SUBMIT를 사용해도 같은 결과 도출.
* LEAVE TO TRANSACTION은 데이터를 넘겨주지 못한다. 내부 세션을 스택에서 삭제하기 때문
  CALL TRANSACTION 'YCHS_16'
  AND SKIP FIRST SCREEN.
```

<br/>

### Shared Buffer

- 데이터 오브젝트를 Cross-Transaction Application Buffer에 저장 가능
- SAP Buffer은 sap 메모리를 사용하기 때문에 모든 메인 세션, 내부 세션 데이터 공유 가능

```
TABLES indx.

DATA : indxkey LIKE indx-srtfd VALUE 'KEYVALUE'.
DATA : gs_flight TYPE sflight.

SELECT SINGLE * INTO GS_FLIGHT
  FROM sflight.

  indx-aedat = sy-datum.
  indx-usera = sy-uname.

  EXPORT GS_FLIGHT TO SHARED BUFFER indx(st) ID indxkey. " export

  CLEAR GS_FLIGHT.
  IMPORT GS_FLIGHT FROM SHARED BUFFER indx(st) ID indxkey. " import

  WRITE : / gs_flight-carrid, gs_flight-connid, gs_flight-fldate.
```

<br/>


### 데이터 삭제

```
TABLES INDX.

DATA GT_ITAB TYPE TABLE OF SFLIGHT.
DATA REPORT TYPE SY-REPID.
DATA GS_WA LIKE LINE OF GT_ITAB.

SELECT * FROM SFLIGHT INTO TABLE GT_ITAB UP TO 5 ROWS.

  EXPORT GT_ITAB TO DATABASE INDX(ZK) ID 'EABAP'.

*  REPORT = 'ZIMPORT_01'.
*
*  SUBMIT (REPORT)
*  AND RETURN.



  IMPORT GT_ITAB FROM DATABASE INDX(ZK) ID 'EABAP'.

  DELETE FROM DATABASE INDX(ZK) ID 'EABAP'.

  LOOP AT GT_ITAB INTO GS_WA.
    WRITE GS_WA-CARRID.
    ENDLOOP.
```


<br/>

- 메모리 ID가 설정된 2개의 파라미터로 구성된 예제

```
PARAMETERS : p_car TYPE sbook-carrid MEMORY ID car,
                       p_con TYPE sbook-connid MEMORY ID con.

WRITE : / P_CAR, P_CON.
```

<br/>

- ABAP 메모리 호출 예제
   

```
DATA : gt_sflight TYPE TABLE OF sflight WITH HEADER LINE.

IMPORT gt_sflight FROM MEMORY ID 'TEST_ID'.

LOOP AT gt_sflight.
  WRITE : / gt_sflight-carrid.
ENDLOOP.
```


<br/><br/><br/><br/><br/><br/><br/><br/><br/>

------------------------------------------------
Reference

- Easy ABAP 2.0 - 김성준 




